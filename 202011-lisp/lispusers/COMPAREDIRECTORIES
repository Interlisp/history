(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "29-Dec-98 08:41:59" {DSK}<project>medley2.0>lispusers>COMPAREDIRECTORIES.;6 21429  

      changes to%:  (FNS COMPAREDIRS.FORMATLINE COMPAREDIRECTORIES)

      previous date%: "29-Dec-98 06:39:50" {DSK}<project>medley2.0>lispusers>COMPAREDIRECTORIES.;3)


(* ; "
Copyright (c) 1985, 1986, 1987, 1988, 1990, 1994, 1998 by Venue & Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT COMPAREDIRECTORIESCOMS)

(RPAQQ COMPAREDIRECTORIESCOMS (
                                   (* ;; "Compare the contents to two directories.")

                                   (FNS COMPAREDIRECTORIES COMPAREDIRS.FORMATLINE 
                                        COMPAREDIRECTORIES.NEWPAGEFN COMPARE-DIRECTORIES)
                                   
                                   (* ;; "look for compiled files older than the sources")

                                   (FNS FIND-UNCOMPILED-FILES)))



(* ;; "Compare the contents to two directories.")

(DEFINEQ

(COMPAREDIRECTORIES
  [LAMBDA (FROMDIR TODIR SHOW=FILESTOO USELISPFILEDATE FILEPATTERN LISTINGFILE EXTENSIONSTOAVOID)
                                                             (* ; "Edited 29-Dec-98 08:39 by rmk:")

    (* ;; "Compare the contents of two directories, e.g., for change-control purposes.  Compares files matching FILEPATTERN (or *.*;) on FROMDIR and TODIR, listing which is newer, or when one is not found on the other.  If SHOW=FILESTOO, then files that are the same are also listed.  LISTINGFILE is either a filename for an image stream, or NIL to go to the display.")

    (LET [(LISTINGSTREAM (COND
                            [LISTINGFILE (OPENIMAGESTREAM LISTINGFILE NIL '(LANDSCAPE T]
                            (T NIL)))
          (TO-FILES (for FILE in [DIRECTORY (PACKFILENAME.STRING 'BODY TODIR 'BODY
                                                           (OR FILEPATTERN '*.*;]
                       collect (PACKFILENAME.STRING 'VERSION NIL 'BODY FILE]
         (COND
            (LISTINGSTREAM (STREAMPROP LISTINGSTREAM 'AFTERNEWPAGEFN #'COMPAREDIRECTORIES.NEWPAGEFN)
                   [STREAMPROP LISTINGSTREAM 'TITLE (CONS (CONCAT FROMDIR " vs " TODIR)
                                                          (CONCAT "as of " (DATE]
                   (COMPAREDIRECTORIES.NEWPAGEFN LISTINGSTREAM)))
         [for FILENAME infiles (PACKFILENAME.STRING 'BODY FROMDIR 'BODY (OR FILEPATTERN
                                                                                    '*.*;))
            bind DT1 DT2 TON SHORT-FROM SHORT-TO
            when [PROGN [SETQ TO-FILES (CL:DELETE (PACKFILENAME.STRING 'HOST NIL 'DIRECTORY NIL
                                                             'DEVICE NIL 'VERSION NIL 'BODY FILENAME)
                                                  TO-FILES :TEST #'(LAMBDA (ITEM SEQUENCE-ITEM)
                                                                     (STRING-EQUAL
                                                                      ITEM
                                                                      (PACKFILENAME.STRING
                                                                       'HOST NIL 'DIRECTORY NIL
                                                                       'DEVICE NIL 'VERSION NIL
                                                                       'BODY SEQUENCE-ITEM]
                            (NOT (CL:MEMBER (UNPACKFILENAME.STRING FILENAME 'EXTENSION)
                                        EXTENSIONSTOAVOID :TEST #'STRING-EQUAL]
            do (AND NIL (PRINTOUT T FILENAME T))
                  (COND
                     [[SETQ TON (INFILEP (PACKFILENAME.STRING 'DIRECTORY TODIR 'VERSION NIL
                                                'BODY
                                                (SETQ SHORT-FROM (PACKFILENAME.STRING 'HOST NIL
                                                                        'DIRECTORY NIL 'DEVICE NIL
                                                                        'BODY FILENAME]
                      (SETQ SHORT-TO (PACKFILENAME.STRING 'HOST NIL 'DIRECTORY NIL 'DEVICE NIL
                                            'BODY TON))
                      [IF (AND USELISPFILEDATE (SETQ DT1 (FILEDATE FILENAME))
                                   (SETQ DT2 (FILEDATE TON)))
                          THEN (SETQ DT1 (IDATE DT1))
                                (SETQ DT2 (IDATE DT2))
                        ELSE (SETQ DT1 (GETFILEINFO FILENAME 'ICREATIONDATE))
                              (SETQ DT2 (GETFILEINFO TON 'ICREATIONDATE]
                      (COND
                         [(EQUAL DT1 DT2)                    (* ; "same")
                          (COND
                             (SHOW=FILESTOO (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM ""
                                                   (GDATE DT1)
                                                   "=="
                                                   (GDATE DT2)
                                                   SHORT-TO ""]
                         (T (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM (GETFILEINFO
                                                                                  FILENAME
                                                                                  'AUTHOR)
                                   (GDATE DT1)
                                   (COND
                                      ((LESSP DT1 DT2)
                                       "< ")
                                      (T " >"))
                                   (GDATE DT2)
                                   SHORT-TO
                                   (GETFILEINFO TON 'AUTHOR]
                     (T (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM (GETFILEINFO
                                                                              FILENAME
                                                                              'AUTHOR)
                               (GETFILEINFO FILENAME 'CREATIONDATE)
                               "**" NIL ""]
         [bind SHORT-TO DT2 for FILENAME in TO-FILES
            when (NOT (CL:MEMBER (UNPACKFILENAME.STRING FILENAME 'EXTENSION)
                                 EXTENSIONSTOAVOID :TEST #'STRING-EQUAL))
            do (SETQ SHORT-TO (PACKFILENAME.STRING 'HOST NIL 'DIRECTORY NIL 'DEVICE NIL
                                         'BODY FILENAME))
                  (SETQ DT2 (GETFILEINFO FILENAME 'ICREATIONDATE))
                  (COMPAREDIRS.FORMATLINE LISTINGSTREAM "" NIL NIL "**" (GDATE DT2)
                         SHORT-TO
                         (GETFILEINFO FILENAME 'AUTHOR]
         (AND LISTINGSTREAM (CLOSEF LISTINGSTREAM])

(COMPAREDIRS.FORMATLINE
  [LAMBDA (STREAM FROM FROMAUTHOR FDATE COMP TDATE TO TOAUTHOR)
                                                             (* ; "Edited 29-Dec-98 08:41 by rmk:")

    (* ;; "Format one line of the directory comparison listing.  If FROMAUTHOR or TOAUTHOR are non-NIL, list the author in parens; otherwise omit it.")

    (COND
       (STREAM                                               (* ; 
                                              "It's an image stream, where TAB doesn't work right.")
              (LET* [(COMPFONT (FONTCREATE 'MODERN 8 'BOLD NIL STREAM))
                     (MAINFONT (FONTCREATE 'MODERN 8 NIL NIL STREAM))
                     (SPACEWIDTH (CHARWIDTH (CHARCODE SPACE)
                                        STREAM))
                     (LEFTMARGIN (DSPLEFTMARGIN NIL STREAM))
                     (RIGHTMARGIN (DSPRIGHTMARGIN NIL STREAM))
                     (CENTER (IQUOTIENT (+ LEFTMARGIN RIGHTMARGIN)
                                    2))
                     (COMPWIDTH (IQUOTIENT (IMAX (STRINGWIDTH " >" COMPFONT)
                                                 (STRINGWIDTH "< " COMPFONT)
                                                 (STRINGWIDTH "==" COMPFONT)
                                                 (STRINGWIDTH "**" COMPFONT))
                                       2))
                     (LEFTSIDE (- CENTER 353 COMPWIDTH))
                     (RIGHTSIDE (+ CENTER COMPWIDTH 353))
                     (FROM-STRING (COND
                                     (FROM (CL:FORMAT NIL "~A (~A; ~A)" FROM FROMAUTHOR FDATE))
                                     (T "")))
                     (TO-STRING (COND
                                   (TO (CL:FORMAT NIL "~A (~A; ~A)" TO TOAUTHOR TDATE))
                                   (T ""]
                    (DSPFONT COMPFONT STREAM)
                    (DSPXPOSITION (- CENTER (IQUOTIENT (STRINGWIDTH COMP COMPFONT)
                                                   2))
                           STREAM)
                    (PRIN1 COMP STREAM)
                    (DSPFONT MAINFONT STREAM)
                    (DSPXPOSITION (- LEFTSIDE (STRINGWIDTH FROM-STRING MAINFONT))
                           STREAM)
                    (PRIN1 FROM-STRING STREAM)
                    (DSPXPOSITION RIGHTSIDE STREAM)
                    (PRINTOUT STREAM TO-STRING T)))
       (T                                                    (* ; 
                                                           "the display, where TAB does work.")
          (PRINTOUT STREAM FROM (COND
                                   (FROMAUTHOR (CONCAT "(" FROMAUTHOR ")"))
                                   (T "  "))
                 45
                 (IF FDATE
                     THEN (CONCAT "[" FDATE "]")
                   ELSEIF TDATE
                     THEN (SPACES (IPLUS 2 (NCHARS TDATE)))
                   ELSE "")
                 -2 COMP -2 (IF TDATE
                                THEN (CONCAT "[" TDATE "]")
                              ELSE "")
                 -1 TO (COND
                          (TOAUTHOR (CONCAT "(" TOAUTHOR ")"))
                          (T ""))
                 T])

(COMPAREDIRECTORIES.NEWPAGEFN  [LAMBDA (LISTINGSTREAM)                                (* ; "Edited 15-Nov-88 19:20 by jds")    (* ;; "Print the new-page headings on a COMPARE-DIRECTORIES page.")    (LET* ((LEFT (DSPLEFTMARGIN NIL LISTINGSTREAM))           (RIGHT (DSPRIGHTMARGIN NIL LISTINGSTREAM))           (TITLEFONT (FONTCREATE 'MODERN 10 'BOLD NIL LISTINGSTREAM))           (TITLE (STREAMPROP LISTINGSTREAM 'TITLE))           (HEAD-WIDTH (IQUOTIENT (STRINGWIDTH (CAR TITLE)                                         TITLEFONT)                              2))           (CENTER (IQUOTIENT (+ LEFT RIGHT)                          2)))          (DSPFONT TITLEFONT LISTINGSTREAM)          (MOVETO (- CENTER HEAD-WIDTH)                 (DSPTOPMARGIN NIL LISTINGSTREAM)                 LISTINGSTREAM)          (PRIN1 (CAR TITLE)                 LISTINGSTREAM)          (MOVETO (- RIGHT (STRINGWIDTH (CDR TITLE)                                  TITLEFONT))                 1270 LISTINGSTREAM)          (PRIN1 (CDR TITLE)                 LISTINGSTREAM)          (MOVETO LEFT [IDIFFERENCE (DSPTOPMARGIN NIL LISTINGSTREAM)                              (FIXR (FTIMES 1.5 (FONTPROP TITLEFONT 'HEIGHT]                 LISTINGSTREAM])

(COMPARE-DIRECTORIES
  [LAMBDA (FROMDIR TODIR SHOW=FILESTOO FILEPATTERN LISTINGFILE EXTENSIONSTOAVOID)
                                                             (* ; "Edited  3-Nov-94 15:06 by jds")

    (* ;; "Compare the contents of two directories, e.g., for change-control purposes.  Compares files matching FILEPATTERN (or *.*;) on FROMDIR and TODIR, listing which is newer, or when one is not found on the other.  If SHOW=FILESTOO, then files that are the same are also listed.  LISTINGFILE is either a filename for an interpress master, or NIL to go to the display.")

    (LET ((LISTINGSTREAM (COND
                            [LISTINGFILE (OPENIMAGESTREAM LISTINGFILE 'INTERPRESS '(LANDSCAPE T]
                            (T NIL)))
          FROM-GENERATOR TO-GENERATOR)
         (COND
            (LISTINGSTREAM (STREAMPROP LISTINGSTREAM 'AFTERNEWPAGEFN #'COMPAREDIRECTORIES.NEWPAGEFN)
                   [STREAMPROP LISTINGSTREAM 'TITLE (CONS (CONCAT FROMDIR " vs " TODIR)
                                                          (CONCAT "as of " (DATE]
                   (COMPAREDIRECTORIES.NEWPAGEFN LISTINGSTREAM)))
         [SETQ FROM-GENERATOR (\GENERATEFILES (PACKFILENAME.STRING 'BODY FROMDIR 'BODY
                                                     (OR FILEPATTERN '*.*;))
                                     NIL
                                     '(SORT]
         [SETQ TO-GENERATOR (\GENERATEFILES (PACKFILENAME.STRING 'BODY TODIR 'BODY
                                                   (OR FILEPATTERN '*.*;))
                                   NIL
                                   '(SORT]
         [bind FROM-FILE TO-FILE first (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR))
                                           (SETQ TO-FILE (\GENERATENEXTFILE TO-GENERATOR))
            repeatwhile (OR FROM-FILE TO-FILE) bind DT1 DT2 TON SHORT-FROM SHORT-TO
            do (COND
                      ((AND FROM-FILE (CL:MEMBER (UNPACKFILENAME.STRING FROM-FILE 'EXTENSION)
                                             EXTENSIONSTOAVOID :TEST #'STRING-EQUAL))

                       (* ;; "FROM file is on the prohibited-extension list.  Skip it.")

                       (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR)))
                      ((AND TO-FILE (CL:MEMBER (UNPACKFILENAME.STRING TO-FILE 'EXTENSION)
                                           EXTENSIONSTOAVOID :TEST #'STRING-EQUAL))

                       (* ;; "TO file is on the prohibited-extension list.  Skip it.")

                       (SETQ TO-FILE (\GENERATENEXTFILE TO-GENERATOR)))
                      (T (SETQ SHORT-FROM (PACKFILENAME.STRING 'HOST NIL 'DIRECTORY NIL 'VERSION NIL
                                                 'BODY FROM-FILE))
                         (SETQ SHORT-TO (PACKFILENAME.STRING 'HOST NIL 'DIRECTORY NIL 'VERSION NIL
                                               'BODY TO-FILE))
                         (HELP)
                         (COND
                            ((NOT FROM-FILE)

                             (* ;; " Ran out of FROM files first; print the missing-FROM marker")

                             (COMPAREDIRS.FORMATLINE LISTINGSTREAM "" "" "" "**"
                                    (GETFILEINFO TO-FILE 'CREATIONDATE)
                                    SHORT-TO
                                    (GETFILEINFO TO-FILE 'AUTHOR))
                             (SETQ TO-FILE (\GENERATENEXTFILE TO-GENERATOR)))
                            ((NOT TO-FILE)

                             (* ;; " Ran out of TO files first; print the missing-TO marker")

                             (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM (GETFILEINFO
                                                                                   FROM-FILE
                                                                                   'AUTHOR)
                                    (GETFILEINFO FROM-FILE 'CREATIONDATE)
                                    "**" "" "" "")
                             (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR)))
                            ((CL:STRING-LESSP SHORT-FROM SHORT-TO)

                             (* ;; 
                           "This FROM file has no TO equivalent.  Print the missing-FROM marker")

                             (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM (GETFILEINFO
                                                                                   FROM-FILE
                                                                                   'AUTHOR)
                                    (GETFILEINFO FROM-FILE 'CREATIONDATE)
                                    "**" "" "" "")
                             (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR)))
                            ((CL:STRING-LESSP SHORT-TO SHORT-FROM)

                             (* ;; 
                           "This TO file has no FROM equivalent.  Print the missing-TO marker")

                             (COMPAREDIRS.FORMATLINE LISTINGSTREAM "" "" "" "**"
                                    (GETFILEINFO TO-FILE 'CREATIONDATE)
                                    SHORT-TO
                                    (GETFILEINFO TO-FILE 'AUTHOR))
                             (SETQ TO-FILE (\GENERATENEXTFILE TO-GENERATOR)))
                            ([= (SETQ DT1 (GETFILEINFO FROM-FILE 'ICREATIONDATE))
                                (SETQ DT2 (GETFILEINFO TO-FILE 'ICREATIONDATE]
                             (AND SHOW=FILESTOO (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM
                                                       "" (GDATE DT1)
                                                       "=="
                                                       (GDATE DT2)
                                                       SHORT-TO ""))
                             (SETQ TO-FILE (\GENERATENEXTFILE TO-GENERATOR))
                             (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR)))
                            (T (COMPAREDIRS.FORMATLINE LISTINGSTREAM SHORT-FROM
                                      (GETFILEINFO FROM-FILE 'AUTHOR)
                                      (GDATE DT1)
                                      (COND
                                         ((LESSP DT1 DT2)
                                          "<<")
                                         (T ">>"))
                                      (GDATE DT2)
                                      SHORT-TO
                                      (GETFILEINFO TO-FILE 'AUTHOR))
                               (SETQ TO-FILE (\GENERATENEXTFILE TO-GENERATOR))
                               (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR]
         (AND LISTINGSTREAM (CLOSEF LISTINGSTREAM])
)



(* ;; "look for compiled files older than the sources")

(DEFINEQ

(FIND-UNCOMPILED-FILES
  [LAMBDA (FROMDIR TODIR LISTINGFILE)                    (* ; "Edited  3-Nov-94 15:17 by jds")

    (* ;; "Compare the contents of two directories, e.g., for change-control purposes.  Compares files matching FILEPATTERN (or *.*;) on FROMDIR and TODIR, listing which is newer, or when one is not found on the other.  If SHOW=FILESTOO, then files that are the same are also listed.  LISTINGFILE is either a filename for an interpress master, or NIL to go to the display.")

    (LET ((LISTINGSTREAM (COND
                            [LISTINGFILE (OPENIMAGESTREAM LISTINGFILE 'POSTSCRIPT '(LANDSCAPE T]
                            (T NIL)))
          FROM-GENERATOR TO-GENERATOR)
         (COND
            (LISTINGSTREAM (STREAMPROP LISTINGSTREAM 'AFTERNEWPAGEFN #'COMPAREDIRECTORIES.NEWPAGEFN)
                   [STREAMPROP LISTINGSTREAM 'TITLE (CONS (CONCAT "Compiled-file search of " FROMDIR
                                                                 " vs " TODIR)
                                                          (CONCAT "as of " (DATE]
                   (COMPAREDIRECTORIES.NEWPAGEFN LISTINGSTREAM)))
         [SETQ FROM-GENERATOR (\GENERATEFILES (PACKFILENAME.STRING 'BODY FROMDIR 'BODY "*.;")
                                     NIL
                                     '(SORT]
         (bind FROM-FILE first (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR))
            repeatwhile FROM-FILE bind DT1 DT2 TON SHORT-FROM SHORT-TO
            do (COND
                      [[SETQ TO-FILE (OR (INFILEP (PACKFILENAME 'DIRECTORY TODIR 'VERSION NIL
                                                         'EXTENSION
                                                         'DFASL
                                                         'BODY FROM-FILE))
                                         (INFILEP (PACKFILENAME 'DIRECTORY TODIR 'VERSION NIL
                                                         'EXTENSION
                                                         'LCOM
                                                         'BODY FROM-FILE]
                       (COND
                          ((< (GETFILEINFO TO-FILE 'ICREATIONDATE)
                              (GETFILEINFO FROM-FILE 'ICREATIONDATE))
                           (PRINTOUT LISTINGSTREAM FROM-FILE " (" (GETFILEINFO FROM-FILE 
                                                                         'CREATIONDATE)
                                  ") vs " TO-FILE " (" (GETFILEINFO TO-FILE 'CREATIONDATE)
                                  ")" T]
                      (T (PRINTOUT LISTINGSTREAM FROM-FILE " has no compiled equivalent." T)))
                  (SETQ FROM-FILE (\GENERATENEXTFILE FROM-GENERATOR)))
         (AND LISTINGSTREAM (CLOSEF LISTINGSTREAM])
)
(PUTPROPS COMPAREDIRECTORIES COPYRIGHT ("Venue & Xerox Corporation" 1985 1986 1987 1988 1990 1994 1998
))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1061 18391 (COMPAREDIRECTORIES 1071 . 6944) (COMPAREDIRS.FORMATLINE 6946 . 10232) (
COMPAREDIRECTORIES.NEWPAGEFN 10234 . 11501) (COMPARE-DIRECTORIES 11503 . 18389)) (18456 21300 (
FIND-UNCOMPILED-FILES 18466 . 21298)))))
STOP
