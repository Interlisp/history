@ECHO OFF
@rem ******************************************************
@rem *                                                    *
@rem *     Medley Installation Batch File                 *
@rem *                                                    *
@rem *                                                    *
@rem *   Copyright (c) 1992 Venue.  All Rights Reserved.  *
@rem *                                                    *
@rem *   Usage:  install [dir]                            *
@rem *                                                    *
@rem *     dir is the optional directory to install       *
@rem *     Medley in.  Default is C:\medley               *
@rem *                                                    *
@rem ******************************************************
@rem
set newdir=C:\MEDLEY
if not "%1" == "" set newdir=%1
echo
echo Medley Installer ver 2.01
echo Copyright (C) 1992 Venue.  All Rights Reserved.
echo .
echo Continuing will install Medley on %newdir%.
if not exist %newdir% goto :noms
echo This may destroy old versions of the files there.
:noms
echo (RETURN to continue, ^C to quit)
pause
if exist %newdir% goto direx
echo Creating installation directory %newdir%...
mkdir %newdir% > nul:
echo (done)
:direx

echo copying library to %newdir%...
copy lib.zip/b %newdir% > nul:
copy emul.zip/b %newdir% > nul:
echo (done)

:disk2
echo Please insert the Sysout-1 diskette.
pause
if not exist sysout1.zip goto disk2

echo copying sysout part 1 to %newdir%...
copy sysout1.zip/b %newdir%\sysout1.zip > nul:
echo (done)

:disk3
echo Please insert the Sysout-2 diskette.
pause
if not exist sysout2.zip goto disk3

echo copying sysout part 2 to %newdir%
copy %newdir%\sysout1.zip/b + sysout2.zip %newdir%\sysout.zip > nul:
del %newdir%\sysout1.zip > nul:
echo (done)

:disk4
echo Please insert the Fonts-1 diskette.
pause
if not exist fonts1.zip goto :disk4

echo copying fonts part 1 to %newdir%.
copy fonts1.zip/b %newdir%\fonts1.zip > nul:
echo (done)

:disk5
echo Please insert the Fonts-2 diskette.
pause
if not exist fonts2.zip goto disk5

echo copying fonts part 2 to %newdir%.
copy %newdir%\fonts1.zip/b + fonts2.zip %newdir%\fonts.zip > nul:
del %newdir%\fonts1.zip > nul:
copy pkunzip.exe %newdir% > nul:

echo Uncompressing files...
cd %newdir% > nul:
pkunzip -d %newdir%\emul.zip %newdir% > nul:
del %newdir%\emul.zip > nul:
pkunzip -d %newdir%\lib.zip %newdir% > nul:
del %newdir%\lib.zip > nul:
pkunzip -d %newdir%\sysout.zip %newdir% > nul:
del %newdir%\sysout.zip > nul:
pkunzip -d %newdir%\fonts.zip %newdir% > nul:
del %newdir%\fonts.zip > nul:

echo Installation complete.
