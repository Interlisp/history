(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "20-May-92 14:38:56" {DSK}<users>nilsson>UNIXPRINT.;2 10799  

      changes to%:  (VARS UNIXPRINTCOMS)
                    (FNS UnixPrint UnixPrintCommand)

      previous date%: "20-May-92 14:27:57" {DSK}<users>nilsson>UNIXPRINT.;1)


(* ; "
Copyright (c) 1990, 1991, 1992 by Venue.  All rights reserved.
")

(PRETTYCOMPRINT UNIXPRINTCOMS)

(RPAQQ UNIXPRINTCOMS [(FNS InstallUnixPrinter UnixPrint UnixShellQuote UnixTempFile 
                               UnixPrintCommand)
                          (FUNCTIONS ShellCommand)
                          (INITVARS (UnixPrinterName NIL))
                          (P (InstallUnixPrinter)
                             (PRIN1 "Please feel free to edit UnixPrintCommand." PROMPTWINDOW))
                          (PROP FILETYPE UNIXPRINT)
                          (DECLARE%: DONTEVAL@COMPILE DOCOPY (FNS UnixPrintCommand))
                          (DECLARE%: EVAL@COMPILE DOCOPY (FILES UNIXCOMM))
                          (DECLARE%: EVAL@COMPILE DONTCOPY (GLOBALVARS UnixPrinterName))
                          (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS (ADDVARS
                                                                                         (NLAMA)
                                                                                         (NLAML)
                                                                                         (LAMA])
(DEFINEQ

(InstallUnixPrinter
  [LAMBDA (PrinterTypes)                               (* ; "Edited 14-Feb-91 15:45 by gadener")

    (* ;; "Set up any printers in PrinterTypes (or just Postscript by default) so that they'll be printed using the unix LPR command.")

    (DECLARE (GLOBAL PRINTERTYPES))
    (for type inside (OR PrinterTypes '(POSTSCRIPT))
       do (for x in PRINTERTYPES when (EQMEMB type (CAR x))
                 do (LET ((PRINTERTYPE type))
                             (PUTASSOC 'SEND (LIST 'UnixPrint)
                                    (CDR x])

(UnixPrint
  [LAMBDA (HOST FILE PRINTOPTIONS)                     (* ; "Edited 20-May-92 14:13 by nilsson")

    (* ;; "Given a print FILE, use the Unix %"lpr%" command to spool it to a printer.")

    (* ;; "The printer is named by HOST or UnixPrinterName, a Global variable.")

    [LET* ((PRINTER (OR HOST UnixPrinterName))
           (COPIES (LISTGET PRINTOPTIONS '%#COPIES))
           (NAME (LISTGET PRINTOPTIONS 'DOCUMENT.NAME))
           (TYPE (PRINTERTYPE PRINTER)))

          (* ;; "Removed redundant check (we already know it's a PS printer), JDS 2/19/92:")

          (* ;; "(COND ((NULL TYPE) (ERROR (CONCAT %"Printertype unknown for %" PRINTER))) ((NOT (EQL (U-CASE TYPE) 'POSTSCRIPT)) (ERROR (CONCAT %"Printertype  for %" PRINTER %" is not Postscript%"))))")

          [COND
             ((OR (NULL NAME)
                  (STRPOS "{LPT}" NAME 1 NIL T))
              (SETQ NAME "Medley Output"))
             ((EQ (CHCON1 NAME)
                  (CHARCODE {))
              (SETQ NAME (UNPACKFILENAME.STRING NAME 'NAME))
              (COND
                 ((EQ (NCHARS NAME)
                      0)
                  (SETQ NAME "Medley Output"]
          (CL:MULTIPLE-VALUE-BIND (tmpstream tmpname)
                 (UnixTempFile NIL NIL)
                 (COND
                    (tmpstream 

                           (* ;; "First, copy the lisp file to /tmp so lpr can find it.")

                           (CL:WITH-OPEN-STREAM (out tmpstream)
                                  (CL:WITH-OPEN-STREAM (in (OPENSTREAM FILE 'INPUT))
                                         (printout PROMPTWINDOW .TAB0 0 
                                                "Spooling output to Unix printer"
                                                (COND
                                                   (PRINTER (CONCAT " '" PRINTER "'"))
                                                   (T ""))
                                                "...")
                                         (COPYCHARS in out)))

                           (* ;; "Now make Unix print the /tmp file.")

                           (ShellCommand (UnixPrintCommand PRINTER COPIES NAME tmpname)
                                  PROMPTWINDOW)
                           (printout PROMPTWINDOW "done" T))
                    (T (ERROR "Couldn't create unix temp file"]
    T])

(UnixShellQuote
  [LAMBDA (STRING)
    (DECLARE (LOCALVARS . T))                        (* ; "Edited 19-Apr-89 21:14 by TAL")
    (LET* ((X (CHCON STRING))
           (CT X)
           C FLG)
          [while (LISTP CT) do (SETQ C (CAR CT))
                                      (COND
                                         ([OR (<= (CHARCODE a)
                                                  C
                                                  (CHARCODE z))
                                              (<= (CHARCODE A)
                                                  C
                                                  (CHARCODE Z))
                                              (<= (CHARCODE 0)
                                                  C
                                                  (CHARCODE 9))
                                              (FMEMB C (CHARCODE (- /]
                                          (SETQ CT (CDR CT)))
                                         (T (SETQ FLG T)
                                            (RPLNODE CT (CHARCODE \)
                                                   (CONS (COND
                                                            ((FMEMB C (CHARCODE (CR LF)))
                                                             (CHARCODE SPACE))
                                                            (T C))
                                                         (SETQ CT (CDR CT]
          (COND
             (FLG (CONCATCODES X))
             (T STRING])

(UnixTempFile
  [LAMBDA (Prefix DontOpen)                              (* ; "Edited 12-Jan-89 19:07 by TAL")
    (LET* ([host (AND (BOUNDP 'FISTempDir)
                      (UNPACKFILENAME.STRING FISTempDir 'HOST]
           (dir (OR [COND
                       ((OR (STRING-EQUAL host "UNIX")
                            (STRING-EQUAL host "DSK"))
                        (UNPACKFILENAME.STRING FISTempDir 'DIRECTORY]
                    "tmp"))
           (str (CONCAT (OR Prefix "")
                       (IDATE)))
           file unix)
          (COND
             ([for i from 1 to 100
                 thereis (NOT (INFILEP (SETQ file (CONCAT "{UNIX}"
                                                             (SETQ unix
                                                              (CONCAT "/" dir "/" str i]
              (CL:VALUES [COND
                            (DontOpen file)
                            (T (OPENSTREAM file 'OUTPUT]
                     unix])

(UnixPrintCommand
  [LAMBDA (PRINTER COPIES NAME TMPNAME)                (* ; "Edited 20-May-92 14:26 by nilsson")

    (* ;; "This function is called when the user wants to UNIXPRINT a file. It has to return a string that when sent to a shell prints the file tmpname. In the cub version this should look something like %"/usr/ucb/lpr  tmpname%". The arguments to this function are:")

    (* ;; " PRINTER - the name of the printer. Usually something like lw or  plw.")

    (* ;; "COPIES - how many copies of this job to be printed.")

    (* ;; "NAME - the name of this job. This gets printed on the banner of your job.")

    (* ;; 
  "TMPNAME - The name of the temporary file that contains the postscript code for this job. ")

    (* ;; "Note the clever function UnixShellQuote. It converts any lisp name to a string that is quoted according to /bin/sh syntax")

    (CONCAT "/usr/ucb/lpr " (COND
                               (PRINTER (CONCAT "-P" (UnixShellQuote PRINTER)
                                               " "))
                               (T ""))
           (COND
              ((AND (FIXP COPIES)
                    (NEQ COPIES 1))
               (CONCAT "-#" COPIES " "))
              (T ""))
           " -J"
           (UnixShellQuote NAME)
           " -r -s " TMPNAME])
)

(CL:DEFUN ShellCommand (Cmd &OPTIONAL (Output T))
   (CL:WITH-OPEN-STREAM (s (CREATE-PROCESS-STREAM Cmd))
          (CL:TAGBODY [SETFILEINFO s 'ENDOFSTREAMOP #'(CL:LAMBDA (s)
                                                             (GO OUT]
                 (CL:LOOP (PRINTCCODE (READCCODE s)
                                 Output))
                 OUT))
   NIL)

(RPAQ? UnixPrinterName NIL)

(InstallUnixPrinter)

(PRIN1 "Please feel free to edit UnixPrintCommand." PROMPTWINDOW)

(PUTPROPS UNIXPRINT FILETYPE :COMPILE-FILE)
(DECLARE%: DONTEVAL@COMPILE DOCOPY 
(DEFINEQ

(UnixPrintCommand
  [LAMBDA (PRINTER COPIES NAME TMPNAME)                (* ; "Edited 20-May-92 14:26 by nilsson")

    (* ;; "This function is called when the user wants to UNIXPRINT a file. It has to return a string that when sent to a shell prints the file tmpname. In the cub version this should look something like %"/usr/ucb/lpr  tmpname%". The arguments to this function are:")

    (* ;; " PRINTER - the name of the printer. Usually something like lw or  plw.")

    (* ;; "COPIES - how many copies of this job to be printed.")

    (* ;; "NAME - the name of this job. This gets printed on the banner of your job.")

    (* ;; 
  "TMPNAME - The name of the temporary file that contains the postscript code for this job. ")

    (* ;; "Note the clever function UnixShellQuote. It converts any lisp name to a string that is quoted according to /bin/sh syntax")

    (CONCAT "/usr/ucb/lpr " (COND
                               (PRINTER (CONCAT "-P" (UnixShellQuote PRINTER)
                                               " "))
                               (T ""))
           (COND
              ((AND (FIXP COPIES)
                    (NEQ COPIES 1))
               (CONCAT "-#" COPIES " "))
              (T ""))
           " -J"
           (UnixShellQuote NAME)
           " -r -s " TMPNAME])
)
)
(DECLARE%: EVAL@COMPILE DOCOPY 

(FILESLOAD UNIXCOMM)
)
(DECLARE%: EVAL@COMPILE DONTCOPY 
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(GLOBALVARS UnixPrinterName)
)
)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA )

(ADDTOVAR NLAML )

(ADDTOVAR LAMA )
)
(PUTPROPS UNIXPRINT COPYRIGHT ("Venue" 1990 1991 1992))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1517 8477 (InstallUnixPrinter 1527 . 2132) (UnixPrint 2134 . 4556) (UnixShellQuote 4558
 . 6112) (UnixTempFile 6114 . 7123) (UnixPrintCommand 7125 . 8475)) (9064 10426 (UnixPrintCommand 9074
 . 10424)))))
STOP
