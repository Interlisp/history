(DEFINE-FILE-INFO PACKAGE "INTERLISP" READTABLE "INTERLISP" BASE 10)
(FILECREATED "19-Oct-93 10:48:08" "{Pele:mv:envos}<LispCore>Sources>CLTL2>CMLFLOATARRAY.;1" 29993  

      previous date%: "11-Jun-90 14:41:02" "{Pele:mv:envos}<LispCore>Sources>CMLFLOATARRAY.;1")


(* ; "
Copyright (c) 1985, 1986, 1987, 1990, 1993 by Venue & Xerox Corporation.  All rights reserved.
")

(PRETTYCOMPRINT CMLFLOATARRAYCOMS)

(RPAQQ CMLFLOATARRAYCOMS
       [(DECLARE%: DONTCOPY DOEVAL@COMPILE (FILES (SYSLOAD FROM VALUEOF DIRECTORIES)
                                                  UNBOXEDOPS FLOAT-ARRAY-SUPPORT))
        
        (* ;; "MAPARRAY fns and macros")

        (FNS MAP-ARRAY)
        (FUNCTIONS MAP-ARRAY-1 MAP-ARRAY-2)
        (FUNCTIONS REDUCE-ARRAY EVALUATE-POLYNOMIAL FIND-ARRAY-ELEMENT-INDEX)
        (FUNCTIONS FLATTEN-ARG MAX-ABS MIN-ABS)
        (FUNCTIONS %%MAP-FLOAT-ARRAY-ABS %%MAP-FLOAT-ARRAY-FLOAT %%MAP-FLOAT-ARRAY-MINUS 
               %%MAP-FLOAT-ARRAY-NEGATE %%MAP-FLOAT-ARRAY-PLUS %%MAP-FLOAT-ARRAY-QUOTIENT 
               %%MAP-FLOAT-ARRAY-TIMES %%MAP-FLOAT-ARRAY-TRUNCATE %%REDUCE-FLOAT-ARRAY-MAX 
               %%REDUCE-FLOAT-ARRAY-MAX-ABS %%REDUCE-FLOAT-ARRAY-MIN %%REDUCE-FLOAT-ARRAY-MIN-ABS 
               %%REDUCE-FLOAT-ARRAY-PLUS %%REDUCE-FLOAT-ARRAY-TIMES)
        
        (* ;; "For convenience")

        (PROP FILETYPE CMLFLOATARRAY)
        (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY (LOCALVARS . T))
        (DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS (ADDVARS (NLAMA)
                                                                             (NLAML)
                                                                             (LAMA MAP-ARRAY])
(DECLARE%: DONTCOPY DOEVAL@COMPILE 

(FILESLOAD (SYSLOAD FROM VALUEOF DIRECTORIES)
       UNBOXEDOPS FLOAT-ARRAY-SUPPORT)
)



(* ;; "MAPARRAY fns and macros")

(DEFINEQ

(MAP-ARRAY
  [LAMBDA ARGS                                               (* ; "Edited  9-Apr-87 16:22 by jop")

    (* ;; "First arg, RESULT, may either be an array of the correct type, or a symbol indicating the element-type of the result, or NIL if the map is for effect. Second arg is the mapping functions. Other args are arrays, all of which must have the same number of elements, or non-arrays which will be treated as scalars ")

    (LISP:IF (< ARGS 3)
           (LISP:ERROR "MAPARRAY takes at least three args"))
    (LET ((RESULT (ARG ARGS 1))
          (MAPFN (ARG ARGS 2))
          (ARRAY1 (ARG ARGS 3))
          FIRST-ARRAY)

         (* ;; "Arg checking. First-array is the first array map argument")

         (LISP:IF (NOT (TYPEP MAPFN 'LISP:FUNCTION))
                (LISP:ERROR "Not a function: ~S" MAPFN))
         (LISP:DO ((I 3 (LISP:1+ I))
                   MAP-ARG)
                  ((> I ARGS))
             (SETQ MAP-ARG (ARG ARGS I))
             (LISP:WHEN (LISP:ARRAYP MAP-ARG)
                 (LISP:IF FIRST-ARRAY
                     (LISP:IF (NOT (EQUAL-DIMENSIONS-P MAP-ARG FIRST-ARRAY))
                            (LISP:ERROR "Dimensions mismatch" MAP-ARG))
                     (SETQ FIRST-ARRAY MAP-ARG))))

         (* ;; "Coerce RESULT into an array or NIL")

         (LISP:TYPECASE RESULT
             (LISP:ARRAY (LISP:IF [NOT (OR (EQUAL-DIMENSIONS-P RESULT FIRST-ARRAY)
                                           (AND (NULL FIRST-ARRAY)
                                                (EQ 0 (LISP:ARRAY-RANK RESULT]
                                (LISP:ERROR "Dimensions mismatch: ~S" RESULT)))
             ((OR LISP:SYMBOL CONS) (SETQ RESULT (LISP:IF FIRST-ARRAY
                                                     (LISP:MAKE-ARRAY (LISP:ARRAY-DIMENSIONS 
                                                                             FIRST-ARRAY)
                                                            :ELEMENT-TYPE RESULT)
                                                     (LISP:MAKE-ARRAY NIL :ELEMENT-TYPE RESULT))))
             (T (OR (NULL RESULT)
                    (LISP:ERROR "RESULT must be an array, an element type, or NIL: ~S" RESULT))))
         (LISP:IF FIRST-ARRAY
             (LISP:IF (AND RESULT (< ARGS 5))
                 (LISP:ECASE ARGS
                     (3                                      (* ; 
                                                       "Note: in this case (EQ ARRAY1 FIRST-ARRAY)")
                        (MAP-ARRAY-1 RESULT MAPFN ARRAY1))
                     (4 (MAP-ARRAY-2 RESULT MAPFN ARRAY1 (ARG ARGS 4))))
                 [LET* ((FLATTENED-RESULT (FLATTEN-ARG RESULT))
                        (SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                        [FLATTENED-ARRAYS (for I from 3 to ARGS
                                             collect (FLATTEN-ARG (ARG ARGS I]
                        (ELT-SLICE (LISP:COPY-LIST FLATTENED-ARRAYS))
                        VALUE)
                       (LISP:DOTIMES (INDEX SIZE RESULT)
                           [SETQ VALUE (LISP:APPLY MAPFN (LISP:DO ((%%SUBSLICE ELT-SLICE (CDR 
                                                                                           %%SUBSLICE
                                                                                              ))
                                                                   (%%SUBARRAYS FLATTENED-ARRAYS
                                                                          (CDR %%SUBARRAYS)))
                                                                  ((NULL %%SUBARRAYS)
                                                                   ELT-SLICE)
                                                             (AND (LISP:ARRAYP (CAR %%SUBARRAYS))
                                                                  (RPLACA %%SUBSLICE
                                                                         (LISP:AREF (CAR %%SUBARRAYS)
                                                                                INDEX))))]
                           (LISP:IF RESULT
                               (LISP:SETF (LISP:AREF FLATTENED-RESULT INDEX)
                                      VALUE)))])
             (LISP:IF RESULT
                 [LISP:SETF (LISP:AREF RESULT)
                        (LISP:APPLY MAPFN (for I from 3 to ARGS
                                             collect (ARG ARGS I]
                 (LISP:APPLY MAPFN (for I from 3 to ARGS collect (ARG ARGS I)))))])
)

(LISP:DEFUN MAP-ARRAY-1 (RESULT MAPFN ARRAY)

   (* ;; 
 "Does something fast for MAPFNS - abs truncate float and EXPONENT. ARRAY is always an array.")

   [LET [(RESULT-FLOAT-P (EQ (LISP:ARRAY-ELEMENT-TYPE RESULT)
                             'LISP:SINGLE-FLOAT))
         (ARRAY-FLOAT-P (EQ (LISP:ARRAY-ELEMENT-TYPE ARRAY)
                            'LISP:SINGLE-FLOAT]              (* ; "Coerce MAPFN to standard form")
        (SETQ MAPFN (LISP:TYPECASE MAPFN
                        (LISP:SYMBOL (CASE MAPFN
                                         (MINUS '-)
                                         (FIX 'LISP:TRUNCATE)
                                         (T MAPFN)))
                        (COMPILED-CLOSURE (COND
                                             ((OR (LISP::%%EQCODEP MAPFN '-)
                                                  (LISP::%%EQCODEP MAPFN 'MINUS))
                                              '-)
                                             ((LISP::%%EQCODEP MAPFN 'ABS)
                                              'ABS)
                                             ((OR (LISP::%%EQCODEP MAPFN 'FIX)
                                                  (LISP::%%EQCODEP MAPFN 'LISP:TRUNCATE))
                                              'LISP:TRUNCATE)
                                             ((LISP::%%EQCODEP MAPFN 'FLOAT)
                                              'FLOAT)
                                             (T MAPFN)))
                        (T MAPFN)))
        (COND
           ((AND (EQ MAPFN '-)
                 RESULT-FLOAT-P ARRAY-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-NEGATE RESULT ARRAY))
           ((AND (EQ MAPFN 'ABS)
                 RESULT-FLOAT-P ARRAY-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-ABS RESULT ARRAY))
           ((AND (EQ MAPFN 'LISP:TRUNCATE)
                 ARRAY-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-TRUNCATE RESULT ARRAY))
           ((AND (EQ MAPFN 'FLOAT)
                 RESULT-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-FLOAT RESULT ARRAY))
           (T (LET ((FLATTENED-RESULT (FLATTEN-ARG RESULT))
                    (FLATTENED-ARRAY (FLATTEN-ARG ARRAY)))
                   (LISP:DOTIMES (INDEX (LISP:ARRAY-TOTAL-SIZE RESULT)
                                        RESULT)
                       (LISP:SETF (LISP:AREF FLATTENED-RESULT INDEX)
                              (LISP:FUNCALL MAPFN (LISP:AREF FLATTENED-ARRAY INDEX))))])

(LISP:DEFUN MAP-ARRAY-2 (RESULT MAPFN ARRAY-1 ARRAY-2)

   (* ;; "Does something fast for MAPFNS + - * /. At least one of ARRAY-1 and ARRAY-2 is an array")

   [LET [(ARRAYS-FLOAT-P (AND (EQ (LISP:ARRAY-ELEMENT-TYPE RESULT)
                                  'LISP:SINGLE-FLOAT)
                              [OR (TYPEP ARRAY-1 '(LISP:ARRAY LISP:SINGLE-FLOAT))
                                  (TYPEP ARRAY-1 '(OR FLOAT LISP:RATIONAL]
                              (OR (TYPEP ARRAY-2 '(LISP:ARRAY LISP:SINGLE-FLOAT))
                                  (TYPEP ARRAY-2 '(OR FLOAT LISP:RATIONAL]
                                                             (* ; "Coerce MAPFN to standard form")
        (SETQ MAPFN (LISP:TYPECASE MAPFN
                        (LISP:SYMBOL (CASE MAPFN
                                         (PLUS '+)
                                         (MINUS '-)
                                         (TIMES 'LISP:*)
                                         (QUOTIENT '/)
                                         (T MAPFN)))
                        (COMPILED-CLOSURE (COND
                                             ((OR (LISP::%%EQCODEP MAPFN '+)
                                                  (LISP::%%EQCODEP MAPFN 'PLUS))
                                              '+)
                                             ((OR (LISP::%%EQCODEP MAPFN '-)
                                                  (LISP::%%EQCODEP MAPFN 'MINUS))
                                              '-)
                                             ((OR (LISP::%%EQCODEP MAPFN 'LISP:*)
                                                  (LISP::%%EQCODEP MAPFN 'TIMES))
                                              'LISP:*)
                                             ((OR (LISP::%%EQCODEP MAPFN '/)
                                                  (LISP::%%EQCODEP MAPFN 'QUOTIENT))
                                              '/)
                                             (T MAPFN)))
                        (T MAPFN)))
        (COND
           ((AND (EQ MAPFN '+)
                 ARRAYS-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-PLUS RESULT ARRAY-1 ARRAY-2))
           ((AND (EQ MAPFN '-)
                 ARRAYS-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-MINUS RESULT ARRAY-1 ARRAY-2))
           ((AND (EQ MAPFN 'LISP:*)
                 ARRAYS-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-TIMES RESULT ARRAY-1 ARRAY-2))
           ((AND (EQ MAPFN '/)
                 ARRAYS-FLOAT-P)
            (%%MAP-FLOAT-ARRAY-QUOTIENT RESULT ARRAY-1 ARRAY-2))
           (T (LET ((FLATTENED-RESULT (FLATTEN-ARG RESULT))
                    (FLATTENED-ARRAY-1 (FLATTEN-ARG ARRAY-1))
                    (FLATTENED-ARRAY-2 (FLATTEN-ARG ARRAY-2)))
                   (LISP:IF (LISP:ARRAYP ARRAY-1)
                       (LISP:IF (LISP:ARRAYP ARRAY-2)
                           (LISP:DOTIMES (INDEX (LISP:ARRAY-TOTAL-SIZE RESULT)
                                                RESULT)
                               (LISP:SETF (LISP:AREF FLATTENED-RESULT INDEX)
                                      (LISP:FUNCALL MAPFN (LISP:AREF FLATTENED-ARRAY-1 INDEX)
                                             (LISP:AREF FLATTENED-ARRAY-2 INDEX))))
                           (LISP:DOTIMES (INDEX (LISP:ARRAY-TOTAL-SIZE RESULT)
                                                RESULT)
                               (LISP:SETF (LISP:AREF FLATTENED-RESULT INDEX)
                                      (LISP:FUNCALL MAPFN (LISP:AREF FLATTENED-ARRAY-1 INDEX)
                                             FLATTENED-ARRAY-2))))
                       (LISP:DOTIMES (INDEX (LISP:ARRAY-TOTAL-SIZE RESULT)
                                            RESULT)
                           (LISP:SETF (LISP:AREF FLATTENED-RESULT INDEX)
                                  (LISP:FUNCALL MAPFN FLATTENED-ARRAY-1 (LISP:AREF FLATTENED-ARRAY-2
                                                                               INDEX)))))])

(LISP:DEFUN REDUCE-ARRAY (REDUCTION-FN ARRAY &OPTIONAL (INITIAL-VALUE NIL INITIAL-VALUE-P))
   (SETQ REDUCTION-FN (LISP:TYPECASE REDUCTION-FN
                          (LISP:SYMBOL (CASE REDUCTION-FN
                                           (PLUS '+)
                                           (TIMES 'LISP:*)
                                           (T REDUCTION-FN)))
                          (COMPILED-CLOSURE (COND
                                               ((OR (LISP::%%EQCODEP REDUCTION-FN '+)
                                                    (LISP::%%EQCODEP REDUCTION-FN 'PLUS))
                                                '+)
                                               ((OR (LISP::%%EQCODEP REDUCTION-FN 'LISP:*)
                                                    (LISP::%%EQCODEP REDUCTION-FN 'TIMES))
                                                'LISP:*)
                                               ((LISP::%%EQCODEP REDUCTION-FN 'MIN)
                                                'MIN)
                                               ((LISP::%%EQCODEP REDUCTION-FN 'MAX)
                                                'MAX)
                                               ((LISP::%%EQCODEP REDUCTION-FN 'MIN-ABS)
                                                'MIN-ABS)
                                               ((LISP::%%EQCODEP REDUCTION-FN 'MAX-ABS)
                                                'MAX-ABS)
                                               (T REDUCTION-FN)))
                          (T REDUCTION-FN)))
   (LISP:IF (NOT (LISP:ARRAYP ARRAY))
       (LISP:IF INITIAL-VALUE-P
           (LISP:FUNCALL REDUCTION-FN INITIAL-VALUE ARRAY)
           ARRAY)
       [LET [(SIZE (LISP:ARRAY-TOTAL-SIZE ARRAY))
             (ARRAY-FLOAT-P (EQ (LISP:ARRAY-ELEMENT-TYPE ARRAY)
                                'LISP:SINGLE-FLOAT]
            (CASE SIZE
                (0 (LISP:IF INITIAL-VALUE-P
                       INITIAL-VALUE
                       (LISP:FUNCALL REDUCTION-FN)))
                (1 (LISP:IF INITIAL-VALUE-P
                       (LISP:FUNCALL REDUCTION-FN INITIAL-VALUE (LISP:AREF (FLATTEN-ARG ARRAY)
                                                                       0))
                       (LISP:AREF (FLATTEN-ARG ARRAY)
                              0)))
                (T [COND
                      ((AND (EQ REDUCTION-FN '+)
                            ARRAY-FLOAT-P)
                       (%%REDUCE-FLOAT-ARRAY-PLUS ARRAY INITIAL-VALUE))
                      ((AND (EQ REDUCTION-FN 'LISP:*)
                            ARRAY-FLOAT-P)
                       (%%REDUCE-FLOAT-ARRAY-TIMES ARRAY INITIAL-VALUE))
                      ((AND (EQ REDUCTION-FN 'MIN)
                            ARRAY-FLOAT-P)
                       (%%REDUCE-FLOAT-ARRAY-MIN ARRAY INITIAL-VALUE))
                      ((AND (EQ REDUCTION-FN 'MAX)
                            ARRAY-FLOAT-P)
                       (%%REDUCE-FLOAT-ARRAY-MAX ARRAY INITIAL-VALUE))
                      ((AND (EQ REDUCTION-FN 'MIN-ABS)
                            ARRAY-FLOAT-P)
                       (%%REDUCE-FLOAT-ARRAY-MIN-ABS ARRAY INITIAL-VALUE))
                      ((AND (EQ REDUCTION-FN 'MAX-ABS)
                            ARRAY-FLOAT-P)
                       (%%REDUCE-FLOAT-ARRAY-MAX-ABS ARRAY INITIAL-VALUE))
                      (T (LISP:DO* ((FLATTENED-ARRAY (FLATTEN-ARG ARRAY))
                                    (ACCUMULATOR (LISP:IF INITIAL-VALUE-P
                                                     INITIAL-VALUE
                                                     (LISP:AREF FLATTENED-ARRAY 0)))
                                    (INDEX (LISP:IF INITIAL-VALUE-P
                                               0
                                               1)
                                           (LISP:1+ INDEX)))
                                   ((EQ INDEX SIZE)
                                    ACCUMULATOR)
                             (SETQ ACCUMULATOR (LISP:FUNCALL REDUCTION-FN ACCUMULATOR
                                                      (LISP:AREF FLATTENED-ARRAY INDEX))))]))]))

(LISP:DEFUN EVALUATE-POLYNOMIAL (X COEFFICIENTS)
   (LISP:IF (NOT (LISP:ARRAYP COEFFICIENTS))
       (LISP:ERROR "Not an array: ~S" COEFFICIENTS)
       (LISP:IF (EQ (LISP:ARRAY-ELEMENT-TYPE COEFFICIENTS)
                    'LISP:SINGLE-FLOAT)
           (%%POLY-EVAL (FLOAT X)
                  (%%GET-FLOAT-ARRAY-BASE COEFFICIENTS)
                  (LISP:1- (LISP:ARRAY-TOTAL-SIZE COEFFICIENTS)))
           (LISP:DO ((FLATTENED-ARRAY (FLATTEN-ARG COEFFICIENTS))
                     (INDEX 1 (LISP:1+ INDEX))
                     (SIZE (LISP:ARRAY-TOTAL-SIZE COEFFICIENTS))
                     (PRODUCT (LISP:AREF COEFFICIENTS 0)))
                    ((EQ INDEX SIZE)
                     PRODUCT)
               (SETQ PRODUCT (+ (LISP:* X PRODUCT)
                                (LISP:AREF COEFFICIENTS INDEX)))))))

(LISP:DEFUN FIND-ARRAY-ELEMENT-INDEX (ELEMENT ARRAY)
   (LISP:IF (NOT (LISP:ARRAYP ARRAY))
       (LISP:ERROR "Not an array: ~S" ARRAY)
       (LISP:IF (EQ (LISP:ARRAY-ELEMENT-TYPE ARRAY)
                    'LISP:SINGLE-FLOAT)
           (LISP:DO ((BASE (%%GET-FLOAT-ARRAY-BASE ARRAY)
                           (\ADDBASE BASE 2))
                     (INDEX 0 (LISP:1+ INDEX))
                     (F-ELEMENT (FLOAT ELEMENT))
                     (SIZE (LISP:ARRAY-TOTAL-SIZE ARRAY)))
                    ((EQ INDEX SIZE)
                     NIL)
               (DECLARE (TYPE FLOAT F-ELEMENT))
               (LISP:IF (UFEQP F-ELEMENT (\GETBASEFLOATP BASE 0))
                      (RETURN INDEX)))
           (LISP:DO ((FLATTENED-ARRAY (FLATTEN-ARG ARRAY))
                     (INDEX 0 (LISP:1+ INDEX))
                     (SIZE (LISP:ARRAY-TOTAL-SIZE ARRAY)))
                    ((EQ INDEX SIZE)
                     NIL)
               (LISP:IF (EQL ELEMENT (LISP:AREF FLATTENED-ARRAY INDEX))
                      (RETURN INDEX))))))

(LISP:DEFUN FLATTEN-ARG (ARG)
   (LISP:IF (OR (NOT (LISP:ARRAYP ARG))
                (EQ 1 (LISP:ARRAY-RANK ARG)))
       ARG
       (LISP:MAKE-ARRAY (LISP:ARRAY-TOTAL-SIZE ARG)
              :ELEMENT-TYPE
              (LISP:ARRAY-ELEMENT-TYPE ARG)
              :DISPLACED-TO ARG)))

(LISP:DEFUN MAX-ABS (X Y)
   (LISP:IF (> (ABS X)
               (ABS Y))
       X
       Y))

(LISP:DEFUN MIN-ABS (X Y)
   (LISP:IF (< (ABS X)
               (ABS Y))
       X
       Y))

(LISP:DEFUN %%MAP-FLOAT-ARRAY-ABS (RESULT ARRAY)
   (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
             (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                    (\ADDBASE RESULT-BASE 2))
             (ARRAY-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY)
                    (\ADDBASE ARRAY-BASE 2))
             (INDEX 0 (LISP:1+ INDEX)))
            ((EQ INDEX SIZE)
             RESULT)
       (\PUTBASEFLOATP RESULT-BASE 0 (UFABS (\GETBASEFLOATP ARRAY-BASE 0)))))

(LISP:DEFUN %%MAP-FLOAT-ARRAY-FLOAT (RESULT ARRAY)
   (LET ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT)))
        (LISP:IF (EQUAL (LISP:ARRAY-ELEMENT-TYPE ARRAY)
                        '(LISP:UNSIGNED-BYTE 16))
            (%%BLKSMALLP2FLOAT (%%GET-FLOAT-ARRAY-BASE ARRAY)
                   (%%GET-FLOAT-ARRAY-BASE RESULT)
                   SIZE)
            (LISP:DO ((RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                             (\ADDBASE RESULT-BASE 2))
                      (INDEX 0 (LISP:1+ INDEX)))
                     ((EQ INDEX SIZE))
                (\PUTBASEFLOATP RESULT-BASE 0 (FLOAT (LISP:AREF ARRAY INDEX)))))
        RESULT))

(LISP:DEFUN %%MAP-FLOAT-ARRAY-MINUS (RESULT ARRAY-1 ARRAY-2)
   (LISP:IF (LISP:ARRAYP ARRAY-1)
       (LISP:IF (LISP:ARRAYP ARRAY-2)
           (%%BLKFDIFF (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
                  (%%GET-FLOAT-ARRAY-BASE ARRAY-2)
                  (%%GET-FLOAT-ARRAY-BASE RESULT)
                  (LISP:ARRAY-TOTAL-SIZE RESULT))
           (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                     (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                            (\ADDBASE RESULT-BASE 2))
                     (ARRAY-1-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
                            (\ADDBASE ARRAY-1-BASE 2))
                     (SCALAR (FLOAT ARRAY-2))
                     (INDEX 0 (LISP:1+ INDEX)))
                    ((EQ INDEX SIZE))
               (DECLARE (TYPE FLOATP SCALAR))
               (\PUTBASEFLOATP RESULT-BASE 0 (FDIFFERENCE (\GETBASEFLOATP ARRAY-1-BASE 0)
                                                    SCALAR))))
       (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                 (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                        (\ADDBASE RESULT-BASE 2))
                 (SCALAR (FLOAT ARRAY-1))
                 (ARRAY-2-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-2)
                        (\ADDBASE ARRAY-2-BASE 2))
                 (INDEX 0 (LISP:1+ INDEX)))
                ((EQ INDEX SIZE))
           (DECLARE (TYPE FLOATP SCALAR))
           (\PUTBASEFLOATP RESULT-BASE 0 (FDIFFERENCE SCALAR (\GETBASEFLOATP ARRAY-2-BASE 0)))))
   RESULT)

(LISP:DEFUN %%MAP-FLOAT-ARRAY-NEGATE (RESULT ARRAY)
   (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
             (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                    (\ADDBASE RESULT-BASE 2))
             (ARRAY-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY)
                    (\ADDBASE ARRAY-BASE 2))
             (INDEX 0 (LISP:1+ INDEX)))
            ((EQ INDEX SIZE)
             RESULT)
       (\PUTBASEFLOATP RESULT-BASE 0 (UFMINUS (\GETBASEFLOATP ARRAY-BASE 0)))))

(LISP:DEFUN %%MAP-FLOAT-ARRAY-PLUS (RESULT ARRAY-1 ARRAY-2)
   (LISP:IF (NOT (LISP:ARRAYP ARRAY-1))
          (LISP:ROTATEF ARRAY-1 ARRAY-2))                    (* ; "addition is commutative")
   (LISP:IF (LISP:ARRAYP ARRAY-2)
       (%%BLKFPLUS (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
              (%%GET-FLOAT-ARRAY-BASE ARRAY-2)
              (%%GET-FLOAT-ARRAY-BASE RESULT)
              (LISP:ARRAY-TOTAL-SIZE RESULT))
       (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                 (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                        (\ADDBASE RESULT-BASE 2))
                 (ARRAY-1-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
                        (\ADDBASE ARRAY-1-BASE 2))
                 (SCALAR (FLOAT ARRAY-2))
                 (INDEX 0 (LISP:1+ INDEX)))
                ((EQ INDEX SIZE))
           (DECLARE (TYPE FLOATP SCALAR))
           (\PUTBASEFLOATP RESULT-BASE 0 (FPLUS (\GETBASEFLOATP ARRAY-1-BASE 0)
                                                SCALAR))))
   RESULT)

(LISP:DEFUN %%MAP-FLOAT-ARRAY-QUOTIENT (RESULT ARRAY-1 ARRAY-2)
   (LISP:IF (LISP:ARRAYP ARRAY-1)
       (LISP:IF (LISP:ARRAYP ARRAY-2)
           (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                     (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                            (\ADDBASE RESULT-BASE 2))
                     (ARRAY-1-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
                            (\ADDBASE ARRAY-1-BASE 2))
                     (ARRAY-2-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-2)
                            (\ADDBASE ARRAY-1-BASE 2))
                     (INDEX 0 (LISP:1+ INDEX)))
                    ((EQ INDEX SIZE))
               (\PUTBASEFLOATP RESULT-BASE 0 (FQUOTIENT (\GETBASEFLOATP ARRAY-1-BASE 0)
                                                    (\GETBASEFLOATP ARRAY-2-BASE 0))))
           (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                     (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                            (\ADDBASE RESULT-BASE 2))
                     (ARRAY-1-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
                            (\ADDBASE ARRAY-1-BASE 2))
                     (SCALAR (FLOAT ARRAY-2))
                     (INDEX 0 (LISP:1+ INDEX)))
                    ((EQ INDEX SIZE))
               (DECLARE (TYPE FLOATP SCALAR))
               (\PUTBASEFLOATP RESULT-BASE 0 (FQUOTIENT (\GETBASEFLOATP ARRAY-1-BASE 0)
                                                    SCALAR))))
       (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                 (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                        (\ADDBASE RESULT-BASE 2))
                 (SCALAR (FLOAT ARRAY-1))
                 (ARRAY-2-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-2)
                        (\ADDBASE ARRAY-2-BASE 2))
                 (INDEX 0 (LISP:1+ INDEX)))
                ((EQ INDEX SIZE))
           (DECLARE (TYPE FLOATP SCALAR))
           (\PUTBASEFLOATP RESULT-BASE 0 (FQUOTIENT SCALAR (\GETBASEFLOATP ARRAY-2-BASE 0)))))
   RESULT)

(LISP:DEFUN %%MAP-FLOAT-ARRAY-TIMES (RESULT ARRAY-1 ARRAY-2)
   (LISP:IF (NOT (LISP:ARRAYP ARRAY-1))
          (LISP:ROTATEF ARRAY-1 ARRAY-2))                    (* ; "Multiplication is commutative")
   (LISP:IF (LISP:ARRAYP ARRAY-2)
       (%%BLKFTIMES (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
              (%%GET-FLOAT-ARRAY-BASE ARRAY-2)
              (%%GET-FLOAT-ARRAY-BASE RESULT)
              (LISP:ARRAY-TOTAL-SIZE RESULT))
       (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
                 (RESULT-BASE (%%GET-FLOAT-ARRAY-BASE RESULT)
                        (\ADDBASE RESULT-BASE 2))
                 (ARRAY-1-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY-1)
                        (\ADDBASE ARRAY-1-BASE 2))
                 (SCALAR (FLOAT ARRAY-2))
                 (INDEX 0 (LISP:1+ INDEX)))
                ((EQ INDEX SIZE))
           (DECLARE (TYPE FLOATP SCALAR))
           (\PUTBASEFLOATP RESULT-BASE 0 (FTIMES (\GETBASEFLOATP ARRAY-1-BASE 0)
                                                SCALAR))))
   RESULT)

(LISP:DEFUN %%MAP-FLOAT-ARRAY-TRUNCATE (RESULT ARRAY)
   (LISP:DO ((SIZE (LISP:ARRAY-TOTAL-SIZE RESULT))
             (ARRAY-BASE (%%GET-FLOAT-ARRAY-BASE ARRAY)
                    (\ADDBASE ARRAY-BASE 2))
             (INDEX 0 (LISP:1+ INDEX)))
            ((EQ INDEX SIZE)
             RESULT)
       (LISP:SETF (LISP:AREF RESULT INDEX)
              (UFIX (\GETBASEFLOATP ARRAY-BASE 0)))))

(LISP:DEFUN %%REDUCE-FLOAT-ARRAY-MAX (ARRAY INITIAL-VALUE)
   (LET [(RESULT (LISP:AREF ARRAY (%%BLKFMAX (%%GET-FLOAT-ARRAY-BASE ARRAY)
                                         0
                                         (LISP:ARRAY-TOTAL-SIZE ARRAY]
        (LISP:IF INITIAL-VALUE
            (MAX INITIAL-VALUE RESULT)
            RESULT)))

(LISP:DEFUN %%REDUCE-FLOAT-ARRAY-MAX-ABS (ARRAY INITIAL-VALUE)
   (LET [(RESULT (LISP:AREF ARRAY (%%BLKFABSMAX (%%GET-FLOAT-ARRAY-BASE ARRAY)
                                         0
                                         (LISP:ARRAY-TOTAL-SIZE ARRAY]
        (LISP:IF INITIAL-VALUE
            (MAX-ABS INITIAL-VALUE RESULT)
            RESULT)))

(LISP:DEFUN %%REDUCE-FLOAT-ARRAY-MIN (ARRAY INITIAL-VALUE)
   (LET [(RESULT (LISP:AREF ARRAY (%%BLKFMIN (%%GET-FLOAT-ARRAY-BASE ARRAY)
                                         0
                                         (LISP:ARRAY-TOTAL-SIZE ARRAY]
        (LISP:IF INITIAL-VALUE
            (MIN INITIAL-VALUE RESULT)
            RESULT)))

(LISP:DEFUN %%REDUCE-FLOAT-ARRAY-MIN-ABS (ARRAY INITIAL-VALUE)
   (LET [(RESULT (LISP:AREF ARRAY (%%BLKFABSMIN (%%GET-FLOAT-ARRAY-BASE ARRAY)
                                         0
                                         (LISP:ARRAY-TOTAL-SIZE ARRAY]
        (LISP:IF INITIAL-VALUE
            (MIN-ABS INITIAL-VALUE RESULT)
            RESULT)))

(LISP:DEFUN %%REDUCE-FLOAT-ARRAY-PLUS (ARRAY INITIAL-VALUE)
   (LET [(RESULT (%%POLY-EVAL 1.0 (%%GET-FLOAT-ARRAY-BASE ARRAY)
                        (LISP:1- (LISP:ARRAY-TOTAL-SIZE ARRAY]
        (LISP:IF INITIAL-VALUE
            (+ INITIAL-VALUE RESULT)
            RESULT)))

(LISP:DEFUN %%REDUCE-FLOAT-ARRAY-TIMES (ARRAY INITIAL-VALUE)
   (LET ((TOTAL 1.0))
        (DECLARE (TYPE FLOAT TOTAL))
        (LISP:DO ((I 0 (LISP:1+ I))
                  (BASE (%%GET-FLOAT-ARRAY-BASE ARRAY)
                        (\ADDBASE BASE 2))
                  (SIZE (LISP:ARRAY-TOTAL-SIZE ARRAY)))
                 ((EQ I SIZE)
                  TOTAL)
            (SETQ TOTAL (LISP:* TOTAL (\GETBASEFLOATP BASE 0))))
        (LISP:IF INITIAL-VALUE
            (LISP:* INITIAL-VALUE TOTAL)
            TOTAL)))



(* ;; "For convenience")


(PUTPROPS CMLFLOATARRAY FILETYPE LISP:COMPILE-FILE)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY 
(DECLARE%: DOEVAL@COMPILE DONTCOPY

(LOCALVARS . T)
)
)
(DECLARE%: DONTEVAL@LOAD DOEVAL@COMPILE DONTCOPY COMPILERVARS 

(ADDTOVAR NLAMA )

(ADDTOVAR NLAML )

(ADDTOVAR LAMA MAP-ARRAY)
)
(PUTPROPS CMLFLOATARRAY COPYRIGHT ("Venue & Xerox Corporation" 1985 1986 1987 1990 1993))
(DECLARE%: DONTCOPY
  (FILEMAP (NIL (1897 6534 (MAP-ARRAY 1907 . 6532)))))
STOP
